import java.text.SimpleDateFormat

/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 */

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    def buildSerial = new SimpleDateFormat("MMddHHmm").format(new Date())

    defaultConfig {
        applicationId "io.twoyi"
        minSdkVersion 27
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 27
        versionCode 10354
        versionName "0.5.4-${buildSerial}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
        }
        archivesBaseName = "twoyi_${versionName}"
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task cargoBuild {
    doLast {
        exec {
            //切换到sh文件所在的目录，执行脚本
            workingDir new File(project.projectDir, "rs")
            //执行shell脚本 "," 传参
            commandLine "sh", "build_rs.sh", "--release"
        }
    }
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'com.github.tiann:FreeReflection:3.1.0'

    def libsuVersion = '3.2.1'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"

    // AboutPage
    implementation 'com.github.medyo:android-about-page:1.2.4'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.9.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'moe.feng:AlipayZeroSdk:1.1'

    implementation 'org.jdeferred:jdeferred-android-aar:1.2.4'
    implementation 'com.github.clans:fab:1.6.4'

    //noinspection GradleDependency
    implementation ('com.github.bumptech.glide:glide:4.9.0')
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    annotationProcessor 'androidx.annotation:annotation:1.3.0'

    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.tukaani:xz:1.5'

    implementation 'com.hzy:libp7zip:1.7.0'
//    implementation "com.github.seven332.a7zip:extract:master-SNAPSHOT"
//    implementation 'com.github.omicronapps:7-Zip-JBinding-4Android:Release-16.02-2.02'
//    implementation 'com.victor:lib:1.0.4'
    implementation 'com.cleveroad:androidmanimation:0.9.1'

    def appCenterSdkVersion = '4.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

//    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}